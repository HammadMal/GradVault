<!DOCTYPE html>
<html>
    <head>
        <meta http-equiv="Content-Type" content="text/html;charset=utf-8" />
        <title>Mandelbrot Set</title>
        <script id="vertex-shader" type="x-shader/x-vertex">
            #version 300 es

            in vec2 vPosition;

            out vec2 coordinate;

            void main()
            {
            	gl_Position = vec4(vPosition, 0.0, 1.0);
                coordinate = vPosition;
            }
        </script>
        <script id="fragment-shader" type="x-shader/x-vertex">
            #version 300 es

            precision highp float;

            in vec2 coordinate;

            uniform int nt;

            out vec4 fragColor;

            vec2 sq(vec2 v) {return vec2(v[0] * v[0] - v[1] * v[1], 2.0 * v[0] * v[1]);}

            float normsq(vec2 v) {return v[0] * v[0] + v[1] * v[1];}

            float mandelbrot(vec2 c)
            {
                int count = 0;
                vec2 z = vec2(0.0, 0.0);

                do {
                    z = sq(z) + c;
                    count++;
                }
                while (count < nt && normsq(z) <= 4.0);

                return float(count);
            }

            vec4 map_point(float P, float Q, float R, vec4 A, vec4 B, vec4 C, float X) {
                float a = (X - Q) * (X - R) / ((P - Q) * (P - R));
                float b = (X - P) * (X - R) / ((Q - P) * (Q - R));
                float c = (X - P) * (X - Q) / ((R - P) * (R - Q));

                return a * A + b * B + c * C;
            }

            void main()
            {
                float escape = mandelbrot(2.0 * coordinate);

                vec4 red = vec4(1.0, 0.0, 0.0, 1.0);
                vec4 green = vec4(0.0, 1.0, 0.0, 1.0);
                vec4 blue = vec4(0.0, 0.0, 1.0, 1.0);

            	fragColor = map_point(1.0, float(nt)/5.0, float(nt), red, green, blue, escape);
            }
        </script>
        <script src="http://interactivecomputergraphics.com/8E/Code%20update/Common/initShadersES6.js"></script>
        <script src="http://interactivecomputergraphics.com/8E/Code%20update/Common/MVES6.js"></script>
        <script type="text/javascript" src="map.js"></script>
        <script type="text/javascript" src="mandelbrot-gpu.js"></script>
    </head>
    <body>
        <canvas id="gl-canvas" width="512" height="512">
            Your browser does not support the WebGL.
        </canvas>
    </body>
</html>
